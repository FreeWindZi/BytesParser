group 'com.sht'

apply plugin: 'java'
apply plugin: 'idea'

apply plugin: 'maven'


sourceCompatibility = 1.8
group = 'com.github.FreeWindZi'
//版本属性
version = '0.22'


repositories {
    mavenCentral()
    jcenter()


}




dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

//指定编译的编码

//解决idea 和 Android studio编码的不同
tasks.withType(JavaCompile){
    options.encoding = "UTF-8"
}



jar{
    String someString = ''
    configurations.runtime.each {someString = someString + " lib\\"+it.name} //遍历项目的所有依赖的jar包赋值给变量someString
    manifest {
        attributes 'Main-Class': 'com.sht.bytesparser.BytesParser'
        attributes 'Class-Path': someString
    }

}
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

//清除上次的编译过的文件
task clearPj(type:Delete){
    delete 'build','target'
}

task copyJar(type:Copy){
    from configurations.runtime
    into ('build/libs/lib')

}

//把JAR复制到目标目录
task release(type: Copy,dependsOn: [build,copyJar]) {
    from  'conf'
    into ('build/libs/eachend/conf') // 目标位置
}
